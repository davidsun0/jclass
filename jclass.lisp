(defpackage #:jclass
  (:use :cl))
(in-package #:jclass)

;;; Utility functions

(defun flatten (tree &key (remove-nil nil))
  (if remove-nil
      (labels ((flatten-remove (tree)
		 (cond ((null tree) '())
		       ((atom tree) (list tree))
		       (t (loop for subtree in tree
				if (listp subtree)
				  append (flatten-remove subtree)
				else
				  collect subtree)))))
	(flatten-remove tree))
      (labels ((flatten-keep (tree)
		 (if (atom tree)
		     (list tree)
		     (loop for subtree in tree
			   append (flatten-keep subtree)))))
	(flatten-keep tree))))

(defun u1 (n)
  "Lists the bytes of a 1 byte integer."
  (list (logand #xFF n)))

(defun u2 (n)
  "Lists the bytes of a 2 byte integer in big endian."
  (list (logand #xFF (ash n -8))
	(logand #xFF n)))

(defun u4 (n)
  "Lists the bytes of a 4 byte integer in big endian."
  (list (logand #xFF (ash n -24))
	(logand #xFF (ash n -16))
	(logand #xFF (ash n -8))
	(logand #xFF n)))

(defun access-modifiers (mod-list mod-map)
  (let ((flags (mapcar (lambda (x) (second (assoc x mod-map)))
		       mod-list)))
    (reduce #'logior flags)))

(defparameter *inner-class-modifiers*
  '((:public       #x0001)
    (:private      #x0002)
    (:protected    #x0004)
    (:static       #x0008)
    (:final        #x0010)
    (:interface    #x0200)
    (:abstract     #x0400)
    (:synthetic    #x1000)
    (:annotation   #x2000)
    (:enum         #x4000)))

(defparameter *class-modifiers*
  '((:public       #x0001)
    (:final        #x0010)
    (:super        #x0020)
    (:interface    #x0200)
    (:abstract     #x0400)
    (:synthetic    #x1000)
    (:annotation   #x2000)
    (:enum         #x4000)
    (:module       #x8000)))

(defparameter *method-modifiers*
  '((:public       #x0001)
    (:private      #x0002)
    (:protected    #x0004)
    (:static       #x0008)
    (:final        #x0010)
    (:synchronized #x0020)
    (:bridge       #x0040)
    (:varargs      #x0080)
    (:native       #x0100)
    (:abstract     #x0400)
    (:strict       #x0800)
    (:synthetic    #x1000)))

(defparameter *field-modifiers*
  '((:public       #x0001)
    (:private      #x0002)
    (:protected    #x0004)
    (:static       #x0008)
    (:final        #x0010)
    (:volatile     #x0040)
    (:transient    #x0080)
    (:synthetic    #x1000)
    (:enum         #x4000)))

(eval-when (:compile-toplevel :load-toplevel :execute)
  (defun symbol-concatenate (&rest values)
    (intern (format nil "窿鲠祯弩┅┅换蔑铙翎铘酗镬ㄤ彐篝蝓泗泔铙翎铘痫镬箝癌换磲痧轭骝镯泔铙翎铘麸痫镬轭溴翎忪磲脲栳箬翎忪呼弩у聃犰┅ㄤ彐珏铄蜷蝈箫祧瀛泔铙翎铘痫镬泔铙翎铘豉疱蝈篝溽翎ê滹沲礤铘狒轱⒁邈躜箝鲥禊徜潴泔铙翎铘篝蝓泗溴疱钿孱汩弩麸泔铙翎铘痫镬┅ㄤ彐珏铄蜷泔铙翎铘轭骘怡翦痫镬泔铙翎铘豉疱蝈篝溽翎ê滹沲礤铘狒轱⒚镱鲥螋泔铙翎铘篝蝓泗麸扉篝镦怡翦螽┅ㄤ彐躅痫镬轭溴痫镬泔铙翎铘镳糸镱犰ㄩ铙弪舂⑸铙弪趔泔铙翎铘轭麸泔铙翎铘痫镬戾è轭溴ㄧ弭栳箬泔铙翎铘ㄣ镱篝犷舡痫镬翎忪痫镬┅┅ㄣ镱ㄩ钿屮ㄩ铙弪换遽汨泔铙翎铘磲痼麸轸轭溴轭翳痫镬箦翩ㄧ弭栳箬泔铙翎铘ㄣ镱篝犷舡痫镬翎忪痫镬┅ㄩ钽ㄣ镱篝犷舡痫镬箝痫镬换怡翦泔铙翎铘翎脲赭箪雉ㄩ矧ㄥㄦ轵篝泔铙翎铘ъ镱绛轭骘ㄥㄦ轵篝泔铙翎铘т秕忪瀛轭骘┅暴┅ㄥ蝌矧⒚镱篝犷滹弩铒屮轶轭翳痫镬泔铙翎铘┅┅ㄤ彐躅泔铙翎铘痫镬怡翦痫镬⒚镱鲥螋泔铙翎铘痫镬麸铄篝邃扉篝镦怡翦螽换蝈箫祧犰泔铙翎铘戾è痫镬翎忪ㄣ镱篝犷舡痫镬翎忪痫镬┅换滹瞌盹溟纟翳栳箬翎忪麒殪轸弪狒轭秭弪轸祜镳骘脲轭祜镳骘忮轭翳栳箬脲镦痫镬翎忪泔祆邈氅滹ㄡ痧禊＇蝈箫祧瀛泔铙翎铘痫镬脲┅换箫螋泔铙翎铘怡轭溴戾舄è轭溴泔铙翎铘祜镳骘忮轭翳栳箬脲镦痫镬翎忪躞轭ㄨ狍璀鲠祯雯泔祆邈ㄣ镱氅┅箫螋邃泔铙翎铘箫螋轭溴泔铙翎铘＇弘妁＇汜颟痫镬轸屙磲疸狎＇沅箫螋邃泔铙翎铘螬┅换泔铞弪麸怡翦蠡戾铉翳轶鲍徙趱犰铛礅弪镦泔铙翎铘扉篝醪ūㄣ镱篝犷舡痫镬箝痫镬┅祜镳骘轭痫镬轸屙泔祆邈ㄡ痧禊＇泔铙翎铘轭骘怡翦痫镬皓┅┅ㄤ彐磲泸溴姝赉镱篝犷钺礤翎箪雉怙澌怙澌戾è痫镬ㄧ孱簌愆ㄣ镱篝豉疱ㄧ孱簌愆┅啜痱镧换遽汨篝蝓泗轶扉篝鏖翳翳泔铙翎铘豉疱狍翳骈蝮鲠祯换翳轶轶箫泔铙翎铘汜忮痫螋徕禊躞邃狍栳箬翎忪脲ㄤ彐篝蝓泗ì钺礤ê豉疱扉篝侯犴邃ê泔铙趄蹉麸簌礅镬泔钽狒孱狒⑼了怒钺礤箪雉螬荔祜趔换蝈箫祧泔铙翎铘箴邈獒扉弩镱翳泔铙翎铘豉疱鲩换ㄡ痧禊痫镬＇蝈箫祧瀛泔铙翎铘泔铙翎铘篝蝓泗ㄤ彐礤翳镤蝈箫祧瀛泔铙翎铘ì痫镬ì泔铙舡豉疱ㄥ耢К钺礤┅蝈篝溽翎痫镬轭溴痫镬ㄣ镱К钺礤溽翎┅换溴骈铄醪痫镬轭溴麸蝈箫祧溴疱钿孱汩弩ㄦ戾è醪痫镬轭溴ㄣ镱篝ㄡ痧禊＇蝈箫祧瀛泔铙翎铘痫镬泔铙舂┅ㄤ邈灬蝈ㄩ珙矧徕戾ㄦ躅泗轱醪痫镬轭溴┅ㄤ弩趄蹉趱蜷铉忾钿箪雉溽翎ㄤ邈灬蝈ㄩ珙矧徕戾荔祜趔┅棱镤┅ㄤ彐礤翳镤泔铙翎铘轭骘怡翦ì痫镬ì泔铙舡豉疱ㄥ耢К钺礤┅蝈篝溽翎换溴骈铄醪痫镬轭溴麸珏翳溴疱钿孱泫轭溴ㄦ戾è醪痫镬轭溴ㄣ镱篝醪痫镬轭溴痫镬泔铙铋飑┅ㄤ邈灬蝈ㄩ珙矧徕戾ㄦ躅泗轱醪痫镬轭溴┅ㄤ弩趄蹉趱蜷铉忾钿箪雉溽翎扉篝翎棱镤┅┅┅ㄤ彐赉镱篝犷豸娓轭骘翦舂醪戾铉翳翦舂换麸滹徜篚痧矧骘赵骗脯麒殂梳鲠滹弩换铒翦翳箴邈獒孱泔溟铉骘醌鞍鞍麒殂轶ｘ冒赴磲ъ轶＇汨狎泔溴翦舂ㄤ彐赉镱篝犷轭翦珏颦轭骘鲠祯濠醮鲠祯濠ㄤ彐赉镱篝犷骒镝舡轭骘ㄩ邋瀛忾趔醮殄邋忾趔┅换翳驷泗翳狒祜铉轭骘犷滹踱戾轭骘翎脲赭箪雉轶溴筱蜷忮轭痫镬轭溴ㄤ彐赉镱篝犷祜铉轭骘鲠祯濠醮ㄡ箬鲠祯巢┅醮鲠祯濠ㄤ彐赉镱篝犷滹踱戾轭骘ㄩ邋瀛忾趔醮ㄡ箬殄邋忾趔巢┅醮殄邋忾趔┅ㄤ彐赉镱篝犷沆狍蟓轭骘钺礤醪痫镬轭溴磲脲豸娓轭骘钺礤┅ㄤ彐赉镱篝犷篝蜷铉轭骘翦舂醪痫镬轭溴磲脲豸娓轭骘翦舂┅ㄤ彐赉镱篝犷骈屐洵蝈姝轭骘ㄣ灬篌钺礤钺礤豉疱醪痫镬轭溴磲脲沆狍蟓轭骘沆狍蟓钺礤┅醪痫镬轭溴磲脲钺礤犷洵豉疱轭骘钺礤豉疱┅ㄤ彐赉镱篝犷礤翳镤蝈姝轭骘卑ㄣ灬篌钺礤钺礤豉疱醪痫镬轭溴磲脲沆狍蟓轭骘沆狍蟓钺礤┅醪痫镬轭溴磲脲钺礤犷洵豉疱轭骘钺礤豉疱┅ㄤ彐赉镱篝犷轭翦蜴徙瀛礤翳镤蝈姝轭骘北ㄣ灬篌钺礤钺礤豉疱醪痫镬轭溴磲脲沆狍蟓轭骘沆狍蟓钺礤┅醪痫镬轭溴磲脲钺礤犷洵豉疱轭骘钺礤豉疱┅ㄤ彐赉镱篝犷钺礤犷洵豉疱轭骘辈钺礤豉疱醪痫镬轭溴磲脲豸娓轭骘钺礤┅醪痫镬轭溴磲脲豸娓轭骘豉疱┅ㄤ彐赉镱篝犷礤翳镤栳钿戾轭骘钡腴钿蝈驽蝈钽濠醣腴钿醪痫镬轭溴蝈驽蝈钽濠ㄤ彐赉镱篝犷礤翳镤豉疱轭骘倍ㄤ弩泸轲麸颟醪痫镬轭溴磲脲豸娓轭骘溴筱蜷痿矧┅ㄤ彐赉镱篝犷澌钺黹悱轭骘狈ㄢ镲趔趄狃轭溴钺礤豉疱醪怙雉篝蜥瓠轭溴醪痫镬轭溴磲脲钺礤犷洵豉疱轭骘钺礤豉疱┅ㄤ彐赉镱篝犷轭鲲脲澌钺黹悱轭骘备ㄢ镲趔趄狃轭溴钺礤豉疱醪怙雉篝蜥瓠轭溴醪痫镬轭溴磲脲钺礤犷洵豉疱轭骘钺礤豉疱┅ㄤ彐赉镱篝犷盹漉戾轭骘惫钺礤醪痫镬轭溴磲脲豸娓轭骘钺礤┅ㄤ彐赉镱篝犷疳汶徵瀛轭骘舶钺礤醪痫镬轭溴磲脲豸娓轭骘钺礤┅ㄤ彐珏铄蜷怡翦扉篝痫镬篝蝓泗ê滹沲礤铘狒轱⒚镱篝蝓泗翳忾钺蝙骘蝽镦手篝蝓泗躜瀹┅ㄤ彐磲泸溴姝牦趄蹉钺礤箪雉怙澌怙澌戾è篝蝓泗镡ㄧ孱簌愆痫镬ㄧ孱簌愆┅啜痱镧ㄤ彐篝蝓泗ì钺礤ê泔铙趄蹉麸簌礅镬泔钽狒孱狒⑼了怒钺礤箪雉螬荔祜趔ㄤ彐礤翳镤怡翦扉篝ì痫镬ì篝蝓泗镡钺礤┅ㄦ戾è醪痫镬轭溴ㄣ镱篝醪痫镬轭溴痫镬泔铙舂┅ㄣ镱篝犷舡痫镬ī痫镬篚怏趄蹉趔篚猸扉篝换深箦螋扉篝镦篝蝓泗躜弩轭麸翳秕翦篝蝓泗躜磲疸狎灬礅溽ㄦㄢ翦扉篝痫镬┅篚忪轶舂┅ㄤ邈灬蝈ㄩ珙矧徕戾ㄦ躅泗轱醪痫镬轭溴ㄦ躅泗轱泔铙翎铘痫镬ㄦ躅泗轱篚怏趄蹉趔┅鏖翳箪雉箪雉篝蝓泗镡扉篝棱镤┅┅┅ㄤ彐磲泸轭铄颦篝蝓泗钺礤箪雉怙澌怙澌⒛彐轭弩犷犷镱盹躞轭铄篝蝓泗麒殂轶怩殪怡溴篝蝓泗躜轭扉篝戾è轭铄颦骘蝽ㄧ孱簌愆┅啜磲疸狎灬礅溽ì轭铄颦骘蝽ㄤ弩趄蹉趱蜷铉忾钿箪雉轭铄颦骘蝽扉篝棱镤┅钺礤┅换留趄殁豸篝蝓泗躜弩ㄤ彐磲泸溴姝狒趄殁豸钺礤钺礤篝蜷铉箪雉怙澌怙澌啜溴姝牦趄蹉钺礤箪雉换犰狒趄殁豸弩栳鲥翳轶篝蝓泗躜搴醪钺礤醮戾铉翳怡翦醪痫镬轭溴磲脲豸娓轭骘钺礤篝蜷铉┅戾è怙澌怡翦ㄦ灬趑孱扉篝棱镤候屙秭瀛铋舂┅扉篝醮戾铉翳怙澌怡翦螬怙澌怡翦螬┅ㄤ彐狒趄殁豸怙雉篝蜥瓠礤翳镤⒙镲趔趄狃湾翳镤螈礤翳镤螬醪戾铉翳礤翳镤螬ㄩ铑弪篝蝓泗礤翳镤礤翳镤蝈狎珲礤铘螬醪痫镬轭溴礤翳镤蝈姗醪戾铉翳狎珲礤铘螬篚怏趄蹉趔狎珲礤铘螬┅ㄤ彐狒趄殁豸轭铄颦沆狍箦⑸铑弪渺狍箦螈ㄣ灬篌弩醪戾铉翳沆狍箦螬ㄩ铑弪篝蝓泗沆狍箦ㄩ铑弪沆狍秕翦颦沆狍钺礤骒徵螬醪痫镬轭溴轭铄颦沆狍螬醪痫镬轭溴秕翦颦沆狍螬醪痫镬轭溴钺礤醪ㄡ沣弩蟓盹溟骈弪骒徵轭铄颦沆狍蟓盹溟骈弪螵┅┅换崎屐潴湾翳镤蟋犷渺狍箦ㄤ彐牦趄蹉礤翳镤轭骘ㄦ灬珞钺礤溴筱蜷痿矧狒趄殁豸弩醪ㄡ沣弩蟓盹溟骈弪骒徵礤翳镤盹溟骈弪螵┅醪痫镬轭溴磲脲豸娓轭骘钺礤┅醪痫镬轭溴磲脲豸娓轭骘溴筱蜷痿矧┅醪戾铉翳狒趄殁豸弩┅篚怏趄蹉趔狒趄殁豸弩┅ㄤ彐牦趄蹉骈屐洵轭骘ㄦ灬珞钺礤溴筱蜷痿矧狒趄殁豸弩醪ㄡ沣弩蟓盹溟骈弪骒徵骈屐洵盹溟骈弪螵┅醪痫镬轭溴磲脲豸娓轭骘钺礤┅醪痫镬轭溴磲脲豸娓轭骘溴筱蜷痿矧┅醪戾铉翳狒趄殁豸弩┅篚怏趄蹉趔狒趄殁豸弩┅ㄤ彐牦趄蹉赆鲠沆狍磲觑颦鲥蝮轱黹铒颦鲥蝮轱骒徵钺礤疳蝈铘轭翦蜴徙弩骈屐潴礤翳镤狒趄殁豸弩醪黹铒颦鲥蝮轱瞟醪磲觑颦鲥蝮轱瞟醪ㄡ沣弩蟓盹溟骈弪骒徵沆狍蟓盹溟骈弪螵┅醪痫镬轭溴磲脲沆狍蟓轭骘钺礤┅醪痫镬轭溴磲脲沆狍蟓轭骘疳蝈铘┅醪戾铉翳轭翦蜴徙弩┅篚怏趄蹉趔轭翦蜴徙弩醪戾铉翳骈屐潴┅篚怏趄蹉趔骈屐潴醪戾铉翳礤翳镤螬篚怏趄蹉趔礤翳镤螬醪戾铉翳狒趄殁豸弩┅篚怏趄蹉趔狒趄殁豸弩┅换梳鲠鲥蝮轱ㄤ彐躅赆鲠沆狍蟓怡翦赆鲠沆狍螬戾舄è痫镬磲脲泔铙翎铘痫镬┅ㄢ翦ㄢ翦扉篝痫镬赆鲠沆狍螬┅ㄦ灬趑孱扉篝醮ｘ昧婆铝屡磲玳铛礅弪篚怏羼怡翦穿沆狍鲥蝮轱ㄣ镱篝犷舡痫镬怡翦痫镬篚怏羼怡翦穿候屙秭瀛铋舂┅